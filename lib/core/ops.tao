#! Operator definitions.

import "bool.tao"

# Not

$[lang(not)]
class Not =
    => Output
    => not : Self -> Self.Output

for A < Not member [A] of Not =
    => Output = [A.Output]
    => not = fn
        | [] => []
        \ [x .. xs] => [!x .. !xs]

# Not

$[lang(neg)]
class Neg where =
    => Output
    => neg : Self -> Self.Output

member Nat of Neg =
    => Output = Int
    => neg = fn x => @neg_nat(x)

member Int of Neg =
    => Output = Int
    => neg = fn x => @neg_int(x)

member Real of Neg =
    => Output = Real
    => neg = fn x => @neg_real(x)

for A < Neg member [A] of Neg =
    => Output = [A.Output]
    => neg = fn
        | [] => []
        \ [x .. xs] => [-x .. -xs]

# Add

$[lang(add)]
class Add B =
    => Output
    => add : Self -> B -> Self.Output

member Nat of Add Nat =
    => Output = Nat
    => add = fn x, y => @add_nat(x, y)

member Int of Add Int =
    => Output = Int
    => add = fn x, y => @add_int(x, y)

$[lang(sub)]
class Sub B =
    => Output
    => sub : Self -> B -> Self.Output

member Nat of Sub Nat =
    => Output = Int
    # Hacky, hacky, hacky
    => sub = fn x, y => --x + -y

member Int of Sub Int =
    => Output = Int
    => sub = fn x, y => x + -y

# Mul

$[lang(mul)]
class Mul B =
    => Output
    => mul : Self -> B -> Self.Output

member Nat of Mul Nat =
    => Output = Nat
    => mul = fn x, y => @mul_nat(x, y)

member Int of Mul Int =
    => Output = Int
    => mul = fn x, y => @mul_int(x, y)

# Div

$[lang(div)]
class Div B =
    => Output
    => div : Self -> B -> Self.Output

# And

$[lang(and_)]
class And B =
    => Output
    => and_ : Self -> B -> Self.Output

# Or

$[lang(or_)]
class Or B =
    => Output
    => or_ : Self -> B -> Self.Output

# Eq

$[lang(eq)]
class Eq =
    => eq : Self -> Self -> Bool
    => ne : Self -> Self -> Bool

member Nat of Eq =
    => eq = fn x, y => @eq_nat(x, y)
    => ne = fn x, y => !Self.eq(x, y)

member Char of Eq =
    => eq = fn x, y => @eq_char(x, y)
    => ne = fn x, y => !Self.eq(x, y)

for A < Eq member [A] of Eq =
    => eq = fn
        | [], [] => True
        | [x .. xs], [y .. ys] => x = y and xs = ys
        \ _, _ => False
    => ne = fn x, y => !Self.eq(x, y)

fn eq A < Eq : A -> A -> Bool = x, y => A.eq(x, y)

# Ord

class Ord < Eq =
    => cmp : Self -> Self -> Ordering

fn cmp A < Ord : A -> A -> Ordering = a, b => A.cmp(a, b)

data Ordering =
    | Less
    | More
    \ Equal

member Nat of Ord =
    => cmp = fn x, y => if @less_nat(x, y)
        then Less
        else if @less_nat(y, x)
        then More
        else Equal

member Char of Ord =
    => cmp = fn x, y => cmp(@codepoint_char(x), @codepoint_char(y))

for A < Ord member [A] of Ord =
    => cmp = fn
        | [x .. xs], [y .. ys] => match cmp(x, y) in
            | Less => Less
            | More => More
            \ Equal => cmp(xs, ys)
        | [], [_ ..] => Less
        | [_ ..], [] => More
        \ _, _ => Equal

$[lang(ord_ext)]
class OrdExt < Ord =
    => less : Self -> Self -> Bool
    => less_eq : Self -> Self -> Bool
    => more : Self -> Self -> Bool
    => more_eq : Self -> Self -> Bool

for A < Ord member A of OrdExt =
    => less = fn x, y => match A.cmp(x, y) in
        | Less => True
        \ _ => False
    => less_eq = fn x, y => match A.cmp(x, y) in
        | More => False
        \ _ => True
    => more = fn x, y => match A.cmp(x, y) in
        | More => True
        \ _ => False
    => more_eq = fn x, y => match A.cmp(x, y) in
        | Less => False
        \ _ => True

$[lang(join)]
class Join B =
    => Output
    => join : Self -> B -> Self.Output

for A member [A] of Join [A] =
    => Output = [A]
    => join = fn xs, ys => @join_list(xs, ys)
